Cache for Windows NT^MAC^住所録^~Format=Cache.S~
%RO on Nov 1 2003   2:55 PM
ADBK^MAC^^59474,53391^0
ADBK ;
GetData(Name) [Name, P1, Error, Current, P9] PUBLIC {
 Set P9="",Error=""
 If Name="" Set P9="名前が選択されていません",Error="No Data" Quit
 Set FName=$Piece(Name,"  ",1)
 Set LName=$Piece(Name,"  ",2)
 Set Node="" Set Node=$Order(^ADBK("XNAME",FName,LName,Node))
 If Node="" Set Error="No Data" Quit
 Set Current=Node
 Set Data=^ADBK("DATA",Node,"DEMO")
 Set Phone=^ADBK("DATA",Node,"PHONE")
 Set Name=$Piece(Data,";",1)
 Set Address=$Piece(Data,";",2)
 Set Zip=$Piece(Data,";",5)
 Set Age=($h-$Piece(Data,";",6))\365.25
 Set Dob=$ZDATE($Piece(Data,";",6),3)
 Set Telh=$Piece(Phone,";",1)
 Set Telo=$Piece(Phone,";",2)
 Set delim=$Char(2)
 Set P1=Name_delim_Zip_delim_Address_delim_Telh_delim_Telo_delim_Age_delim_Dob
 Quit 1
}
New() [Name, P1, Error, Current] PUBLIC {
 Kill Current
 Quit 1
}
Kill() [Name, P1, Current, Error] PUBLIC {
 Lock +^ADBK("DATA",Current)
 Set Name=$Piece(^ADBK("DATA",Current,"DEMO"),";",1)
 Kill ^ADBK("DATA",Current)
 Kill ^ADBK("XNAME",$Piece(Name,"　",1),$Piece(Name,"　",2),Current)
 Lock -^ADBK("DATA",Current)
 Quit 1 
}       
Update(P0) [Name, P1, Error, Current, P9] PUBLIC {
 Set P9="",Error=""
 Set New=0
 If '$Data(Current) {
   Set New=1
   Lock +^ADBK("COUNT")
   Set ^ADBK("COUNT")=^ADBK("COUNT")+1
   Lock -^ADBK("COUNT")
   Set Current=^ADBK("COUNT")
 }
 Set Name=$Piece(P0,$Char(1),1)
 Set Address=$Piece(P0,$Char(1),2)
 Set Zip=$Piece(P0,$Char(1),3)
 If Zip'="",Zip'?3n1"-"4n Set P9="郵便番号は, 999-9999の形式で入力" Quit 0
 Set Age=$Piece(P0,$Char(1),4)
 If New=1,Age'="" Set P9="登録時には、年齢の入力は、不要" Quit 0
 Set DOB=$Piece(P0,$Char(1),5)
 If DOB'="",DOB'?4n1"-"2n1"-"2n Set P9="誕生日は、YYYY-MM-DDの形式" Quit 0
 If DOB'="" Set DOB=$ZDATEH(DOB,3)
 Set Age=($h-DOB)\365.25
 Set Telh=$Piece(P0,$Char(1),6)
 Set Telo=$Piece(P0,$char(1),7)
 If Telo'="",Telo'?.n1"-".n1"-".n Set P9="電話番号は、999-9999-9999の形式で入力" Quit 0
 If Telh'="",Telh'?.n1"-".n1"-".n Set P9="電話番号は、999-9999-9999の形式で入力" Quit 0
 Set LName=$Piece(Name,"　",1)
 Set FName=$Piece(Name,"　",2)
 If FName="" Set P9="名前は、全角スペースで区切って下さい。" Quit 0
 Lock +^ADBK("DATA",Current)
 Set ^ADBK("DATA",Current,"DEMO")=Name_";"_Address_";;;"_Zip_";"_DOB
 Set ^ADBK("DATA",Current,"PHONE")=Telh_";"_Telo_";"
 Set ^ADBK("XNAME",LName,FName,Current)=""
 Lock -^ADBK("DATA",Current)
 Quit 1                
}

VISMUTIL^MAC^^59474,50200^0
GetList(Gname,Node,Matchkey,Delim) [PLIST, i, Node2, Matched] PUBLIC {
 //　グローバルからリストを取得し、PLISTに返す。
 // Gname  --- グローバル名+固定サブスクリプト(入力必須)
 // Node   --- 検索開始ノード　（必須）
 // Matchkey    --- 検索条件　(必須）
 // NoofNodeLevel --- 持ってくるサブスクリプトのレベル < 3 (必須)
 // Delim       --- 出力リスト項目の区切り文字 (Default $Char(2))
 // Return Value --- 1
 //        Error --- 1 OK
 //              --- 0 Error Description
 Kill PLIST
 If Node'="" Set Node=$Extract(Node,1,$Length(Node)-1)_$Char($Ascii($Extract(Node,$Length(Node)))-1)
 Set Matched=0
 If '$Data(Delim) Set Delim=$Char(2)
 For i=1:1 { 
   Set Node=$Order(@Gname@(Node))
   If Node="" Quit
   If Node'[Matchkey Set Matched=1,Node="" Quit
   Set Node2=""
   For j=1:1 {
	 Set Node2=$Order(@Gname@(Node,Node2))
	 If Node2="" Quit  
     Set PLIST(i)=Node_Delim_Node2
   }  
 }
 Set Error="1 OK"
 Quit 1
}



